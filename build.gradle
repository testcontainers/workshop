plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

repositories {
    mavenCentral()
}

sourceSets {
    acceptanceTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    acceptanceTestImplementation.extendsFrom testImplementation
    acceptanceTestRuntime.extendsFrom testRuntime
}

dependencyManagement {
    imports {
        mavenBom 'org.testcontainers:testcontainers-bom:1.16.2'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.kafka:spring-kafka'

    runtimeOnly 'org.postgresql:postgresql'

    testRuntimeOnly 'com.h2database:h2'

    testImplementation 'io.rest-assured:rest-assured:3.1.0'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:kafka'
    testImplementation 'org.awaitility:awaitility:3.0.0'

    acceptanceTestImplementation 'org.testcontainers:selenium'
    acceptanceTestImplementation 'org.seleniumhq.selenium:selenium-java:3.13.0'
}

task acceptanceTest(type: Test) {
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn acceptanceTest

bootJar {
    baseName = 'app'
}
sourceSets.acceptanceTest.resources.srcDir jar.outputs.files.singleFile.parentFile

test {
    environment "LOG_DATEFORMAT_PATTERN", "HH:mm:ss.SSS"
    useJUnitPlatform()
}
